// SPDX-License-Identifier: MIT

/**
  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .  . .  .
   .       .       .   . . . .   .       .       .       .       .       .       .       .       .       .       .       .       .       .       . 
     .  .    .  .    ;8:t@88Xt;8:  .  .    . .     . .     . .     . .     . .     . .     . .     . .     . .     . .     . .     . .     . .     
 .       .       :SX% .88@SS@8@:X;X.    .      .       .       .       .       .       .       .       .       .       .       .       .       .   
   .  .    .  .;@8;XXS;t%%%%%%%%XX  t.    .  .   . .    . .  .   . .    . .  .   . .    . .  .   . .    . .  .   . .    . .  .   . .    . .  .   . 
  .    .  .  .8%tt;S;XtX%8t8t@t%%%Xt;:.    .   .     .         .     .         .     .         .     .         .     .         .     .         .   
    .      .X: 8t%SXtStXtt@;St@SS%t8 X8% .%S:..t@X;..  .  . .    .    .  . . .    .   .  . . .    .   .  . . .    .   .  . . .    .   .  . . .    .
  .   . . %; 8S;X%SSSX%%@t%Xt%ttt88S SX @;X;Xt8X@8; X.8X%:.:;S@88@t:            .   .           .   .           .   . .:;::     .   .           .  
    .    t:S%;S%%X%8tX@8@X8S%SS8%X 8. ::8::;;t;:...:;S8 X;X%:8@S%8tt8;. .  .  .    .t@8@t. .  .        .  .  .    .tX8X; .%8@%:        .  .  .    .
  .    .%SS;XtX%XtX;@XX.XXt.@t8.8;.. . 88X.;;tt8. ttt;;t8 %t;;@88@ X8%@t     .   .%%X888X;S.    . .  .   .     .:8:tXXS%t%SSX%.t8S. .   .      .   
     ..S88tXt%%Xt@;8:%:.:8S@.8. . . . ..XX@:;tt8 X:8@@;;8:t. .;%%: .X;8SS..    . :X8t888tX@%..     .   .   . .:8t8.X88@8@@@88@;8 S% X:    . .    . 
  .   t;XStXt@tXt%@S;; ;tS@X. .     . . .8Xttt;;ttXX@X;;t%. .;.@SSt ...8:t  .   .S88t@@8888@8:  .            ::8@8@888888888888X S:@ 8: .     .    
    . 888tStStS%X%%S;.%:S X..  . . .     .8S@;:;;;;;;;;S%t  t.@ X;..S%X;S8.   . ;t8;S8X8t@.8t;    .  .  . . t;8;88888888888888888.X:@;.;   .   .  .
  .   % X%XtX%@%8. :.X%X8:   .   . . .  . .:@. tXSt@8;8S.  .8.88S%SSX8:.8.%. .;8S@:%.::SXSXXXt .    .      ttSt@888888888888888888@8 :S.:.   .     
     .S @%XtSXt8 X: ;S8%: .    .     . . .  . t8t:;:t8t  . ;t@t@X@.%  :XS@8..@8SX8: .  :tSXX8t   .    . .  S8:8X888888888888888888;; .;@8  .    .  
  .   Xt8%XS%8S@8:.:@8;; . . .  . .   . .      ..;;:.. .  .t 8;%88.t X8Xt@:t88:@% .   . :888X: .   .     .::@XX@888888888888888888S ;:;tt.    .   .
   .   ;t8:t ;8..  @8;8 .     .    . .   .  .     .   . .  :8@ t@;.8X%8S%8.t8:. .   . ;XS8.X     .   .   .8S8%%X888888888888888888@ t;:8X. .    .  
     .   t88t.   .:% %:  . .   ..   .  .  t8X88@.  .  . . . .;:.   88t@88: .    . . .8t@X8. . .       .  .%@X;@8888888888888888888S ;;;@X.   .     
 .     .       .  S @8 .  .  .   .  .    .: % SSSt8.     .   . .. %.SS:S X8   . . .X.8XSt       . . .   ..%@X;@888888888888888888@ ; :;S%. .   .  .
   . .     . .   .88tX.%SSt. . . .    .  .XX888Stt;... .  .     ;8X8;;88;;; .   :8t8t8;    .  .          .8X8t%88888888888888888@8 @.;;;%.   .     
  .     .      .  %@:@:8SXX%%.. . . .     .%  t@88 S. .   . ..S@.X8%..S:SX  . .X. @8    .   .    .  . .  .: @%88888888888888888@8:X.:;88@ .     .  
     .   .  .     SS tX;;;t@88    .    . . . ;8%.t@: . .  . .;tXStX ;;:.X:  .@ S..@S8;.   .    .        ..@%%.8@888888888888888@; ::;%;:    .  .  .
  .    .      . .%tX;;tSXt%t@tS .  . .  .          . .  .    .;;.  S.@ ;. . SX@;88S.X8S:8S;.  .   .  .:S%X@.X88888888888888888X  XX%tS.t .         
    .    . .   .;XXtS@%XtXt%8;t . .   .    .  .  .. .    .       .t;X:.t   .:%;.. ..:X;8 X.8:.   .  .;X8 ;8X8888888888888888888S .:@ %X@S;.  .  . .
  .   .      . XXXS%StXtSt%X;S.  .  . . . .    .   .  . .  . . ..X8t%;@: . . .       .:;%XS8;@.    %XS8:88888888888888888888888888888.SSSX;S.      
    .   .  .  @S8;StXtXtXtXS%8; .  .  .  .  . . . . .  .     ..X :SX;@: .   . . . ..    . .8;Xt;.%;XXt88888888888888888888888888888888Xt@ t ;:. .  
  .           ;;tStSSStXt;88t8X  .  .  .   . . . .   . . . ..%8;%@tS8;.  .  .    .   .  .   St8t888tS%@88X8888888888888888888888888888@88@;t:t;    
     . . .  .%@SStSX%Xt%%%..%XX  .  .    .    .  .   .   .;S8St;;:%8t   . .   . .  .  .  . ..@Xt %%%;8 S Xt8888888888888888888888888888St t:@ X. . 
  .        ..; ;%@tX%%@%XX  %@S   .  . .    .  .   .  :t@8St;;t;;%@:  .   . .  .     .   .   .@8  SX8 @8S@:8888888888888888888888888888@8S8.:8%S   
    .  .    .SXXt%Xt@%%SXX@ X@; .  .   . .   .  . ..;%8Xt:;;tt;;t@:  . . . .  . . . . ..    . .t8@8S:    X  :@8888888888888888888888888@8.8.:t 8.  
  .      . .SX@t@tXt%@XtXX8.88 8;..  .  .  . ..:tS@8S:@@:ttttt;%8:  . .  .    .     .   . .     .     . .;   @8888888888888888888888888@8 8.:X%.  .
    . .    t.t;%%S%X%%%X@@t:;8.8@t;8Xt:. ::t@8@t.%@ @t t;ttt;;XX.      .   . .  . .  . ;S.  . .     .   ;t. @88888888888888888888888@:;88%8.:;8:   
  .     . .@ 8SXSS%X%@tSS88%   tX%88Xt... ;%X888@ X;:t@Xtt;t;t8.  .  .   .    .  .    S:X%t  . .  ;@@@; 8 .S888888888888888888888888%:t@88S.;% X . 
     .    @S8tX;tXtStSSX%88S.    .t8%.tX@888@X;:%X X@@%;tt%ttSS .  .  .   . .     . .tS:8.X8:. ;8:8  :X:Xt8%88888888888888888888888S..8S:%:.;888   
  .    .%  @St%X%StXttStX8@t   .      ..::t;:.:88X8:.:t8%S X%X%  .  .  .  . .  .   :88t S%   8X   8:.@ S@;%888888@8% X@8X8StS888@S. t8S.; .:X:X:  .
    . :@88;XtX%%XtSt@t%Xt%t  .    .    .     .X88X  . .8@ 88 X8.  .  .    .  .   .:888 ;%;t:8SX@:tt;:t%t;t88888@8: X .888@@:. :..: XX@tS..:t%tt  . 
  .  .X 8tSXtXtSXtXt%@%St X     .   .   .:. ;%8XX.. .t;88@t;888 @.      . . .  .;SS@X .t.:.:;;;;:.:.....%888888.S8;:;;@ @;t88SS%X@8Xt 8::;@.%t.    
    .% @tX%%S%XSStXt@tXSX t.  .       :8@8@S8@:;. .St88%;    :8;8StX;.. .   ..%@@;.%@t;.:.:......:...:t@88888%:t::t8%88;%X.X8888X t%t;;X8.:@.   .  
  . .@8tXtXX%XtXtStXtS;8 @  .    . .  t. %8 :8:  .8X ;X.  . . .:Xt;SX@t.  ..t@8tX@;8t..:::.::.:.:...;X8888@  ;:;;S88@:   .:;SXS @S;%@8;@:%:   .   .
    .@8tXt%%SXtXtXtXtX@ 8;     .     ..8S@.8t. ;8:8t8; .t@8S%8@%SS   t88  .@.8X.8%8t:::...:..:....:S888@8: @:;;X@X%%.        .;X8%. :X8t:   .      
  .  t %%@S@t%StStS%@8@8  .  .   .      :8;8X:;X8 @. ;8;8X%::;S8 S 8tS @ . S:@8 S ;S;;:.:.::....;S888@8t.S::t@t@8.    . .  .           .  .    . . 
    .t%8%X%%8%XtX%@%888 .      .   .  .   ;8%. S@.  S%8tS888@%   @88::Xt.  .;88%;% X X ;.:.....%888X8. X.:;t@8X;   .        .      .         .     
  .  .888%@%SXt@t@8.:X.    . .      .   .   .;:.  :88@t8@888@:  X:SXtS;;     :SX% XX :8.;:...tX888@;X8:.:S@S:S.   .  .  . .   . .   . . .  .    .  
      .%:XS8888X.8%%.  .  .     . .             ..;8;888888XX.888:8   t S.  .  .  ::%S :t..%@88@8. X.:;@t88..                .    .       .   .   .
  .      SS:XX;;8:   .       .       . .  . . . .8@8@8888@8@8S @   t@@8: .%X@@8S:  % S@.;;X88X@;X8.:;;@X%:     . .  . .  .  .   .    .  .   .   .  
   . .  .   ::.        . .  .  .  .      .      tSX%@888888.888@t;8@@8@:S       tt.X ;XSX88@@S8X::;t8 ;S     .     .    .     .    .          .    
         . . .  .  .             .  .  .    .  ;8@:@8888888888888888888@t::::::%t88  .@8888;S8::tXX8@:. .  .    .     .   .      .   . . . .     . 
 .  . .           .  .  . .  . .          .    %:8:S888888888888888888%. ....:t; t%@S88888XX..;t8 tt          .   .     .   .  .   .         .  .  
        . .  . .      .           .  . .    .  S:8;X88888888888888888XttSSt....:;.t888@8:.S.:;8:tX.  .  .  .    .   . .      .   .    .  .         
  . .      .     .  .    . .  .    .     .    .tt8 S88888888888888888888888X%t%S88888@Xt;.:;8  X.           .            . .        .      . . .  .
      . .    .    .    .     .  .    .     .   .X@t88888888888888888888888888888888@; ;.;;%%@S.   . . .  .    .  . .  .       . .     . .          
 .  .     .    .    .     .      .     . .   .  :.@tS%S8888@8888888888888888888888%88;;;%X8%; .         .  .   .     .  .  .      . .     .  .  .  
      .     .   .     . .   .  .   . .     .    ..XX8:S%tt:t888888888888888888@8: 8.:;SS88:.    .  .  .      .   .          .  .       .    .    . 
  . .   .     .   . .     .      .     .    . .   :X @8::..S88888888888888888S888;:;t8%;t     .     .    .  .      .  .  .    .  .  .    .    .    
         . .    .      .     . .    . .t@S:@888@.8%:;8%tS%8X@8888888888888888: S.;;8%%@.  . .   . .    .   .   .  .    .   .       .  .    .    .  
 .  . .      .     .  .   .       . .X. .88XXSS;@8  t..88:8X888888888888888%t;.:;8 :@. .      .      .   .    .     .    .   .  .       .    .    .
        .  .   .  .     .   .  .    %t.8@@tt;;t%Xt@:S:X.88X88888888888888: @..:SXS@.     .  .    .  .       .   .    .     .     .  .    .    .    
  .  .    .         .     .    ..t8.8:8888%t;;%t%SXS ;;8 8888888888888888@8 :;8 %%   .         .      . . .      . .   .      .      . .   .    .  
   .   .    . . .    . .    ..SSSS.S@8@88Sttt;t;%%SS t%@88888888888888888;t.:; @.  .   .  .  .   . .         . .        . .  .  . .       .  .   . 
 .    .  .        .      . .%SX :SSSXX8X:;ttttt%t%X.8S8X88888888888888888@ t:ttt.       .   .        .  .  .      .  .              .  .       .   
    .      .  .  .  .  .  %X%S .tStXS8tttt%%%ttt8X@@;t@S888888888888888888SS:;@8;.  . .   .   . .  .         .  .     .  . .  .  .       .  .     .
  .    .                 .;8S;X.;tSS8Xtt;tXt%S8888@@. .8t88888888888888888SS.;88;                    . . . .      . .        .     . . .     .  .  
    .    . . . .  . . .  .:X:8  ..;8t::;%88888888X@   @S%88888888888888@88 ;;;88;. .  . .  . .  . .           . .      .  .    . .       . .      .
  .   .          .      .t@t@S S tX:ttX8@XX88@:. t;.  % SX@88888888888S:tt ::;88;    .    .         .  .  .  .     .  .     .       .  .      . .  
        .  .  .     . ..%S%8@@S.X;XX;SSXXSXS :%%St     88;@X888888888@.;@88.;tt:  .     .    . .  .             .       . .   .  .        .  .     
  . . .   .     .     t8%8888 X:S%:..;S@@@S%@;.::  . . ;XX.X888888888 ;X 8.:tt88.   .      .        . . .  . .    . .       .      . .  .       .  
        .   .  .  ..;8t..X8S.88S       ::X.@:          .t;X.@;t@88Xt.;X:t::;X;::      . .    .  . .       .    .      . .      . .     .   .  .   .
  .  .            .t88@8:@:@8 X8@X8X;@.:8t    .  .  .   .;;8X:%tt;;t:S.t.:;8.@:  . .      .          .  .    .   .  .     .  .      .    .   .     
       .  . . .  . ::;8% 88.X88@XX;@:;t;       .      .   :8:88@%%8 %t;;;t@88..      .  .   . . .  .       .   .      .    .    .  .  .    .   . . 
  . .    .         .tS8%St X;@8%t@;  .  .  . .   . .      ..;8%% S8S:tX@S@:S.  .  .   .   .          . . .        . .   .    .          .          
      .     .  . .S%8;8%%S%8SS8:. .   .              .  .   . . :8:tS% @%:.     .   .        .  . .         . .  .       .     . .  .     .  .  .  
  .    .  .     ;8% 8% X.8:.t.      .    . .  . .  .   .        .    .       .    .    . .          .  .  .         . .    . .     .  . .     .   .
    .       . :X;S;t tt8t.     . .     .             .    .  .    .     .  .   .     .     . . .  .         .  .  .     .       .          .    .  
  .   . . . :X S. 8;8t: .  . .     . .   .  . .  . .    .   .  .    . .      .   .  .   .           . . .     .     .     .  .    .  .  .   .      
    .      ;8:X  8% .          .       .        .     .   .      .      . .    .      .   .  .  . .       . .   . .   . .     .     .    .    .  . 
  .    .  .t8@8t:..    .  .  .   . .      . .      .         .    .  .      .     .     .     .      .  .                 . .   . .   .    .   .   
    .    .     .     .     .         .  .     .  .   . . .    . .      .  .   .  .  .     . .    .        .  . .  . .  .                .    .     
  .   .     .   .  .    .     . .  .   .   .   .           .       . .      .        . .       .   . . .             .   .  . . .  . .    .     . .
*/

pragma solidity ^0.8.4;

/**
 * @dev Interface of the BEP20 standard. 
 */
interface IBEP20 {
     /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

contract RocketDoge is Context, IBEP20 {
    mapping (address => uint256) private _balances;
    mapping (address => mapping (address => uint256)) private _allowances;

    string private _name = "RocketDoge";
    string private _symbol = "RD";
    uint8 private _decimals = 18;
    uint256 private _initialSupply = 1000000 ether;
    uint256 private _totalSupply;

    /**
     * Initialize the token.
     */
    constructor () {
         _totalSupply += _initialSupply;
        _balances[_msgSender()] += _initialSupply;
        emit Transfer(address(0), _msgSender(), _initialSupply);
    }
    
    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5,05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {BEP20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IBEP20-balanceOf} and {IBEP20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return _decimals;
    }

    /**
     * @dev See {IBEP20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IBEP20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IBEP20-transfer}.
     *
     * Requirements:
     *
     * - `recipient` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev See {IBEP20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IBEP20-approve}.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev See {IBEP20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * Requirements:
     *
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for ``sender``'s tokens of at least
     * `amount`.
     */
    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "RD: transfer amount exceeds allowance");
        unchecked {
            _approve(sender, _msgSender(), currentAllowance - amount);
        }

        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IBEP20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IBEP20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "RD: decreased allowance below zero");
        unchecked {
            _approve(_msgSender(), spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves tokens `amount` from `sender` to `recipient`.
     *
     * This is internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `sender` cannot be the zero address.
     * - `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     */
    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "RD: transfer from the zero address");
        require(recipient != address(0), "RD: transfer to the zero address");

        uint256 senderBalance = _balances[sender];
        require(senderBalance >= amount, "RD: transfer amount exceeds balance");
        unchecked {
            _balances[sender] = senderBalance - amount;
        }
        _balances[recipient] += amount;

        emit Transfer(sender, recipient, amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "RD: approve from the zero address");
        require(spender != address(0), "RD: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }
}